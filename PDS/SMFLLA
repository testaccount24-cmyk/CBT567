//SAS      EXEC SAS
//FT11F001 DD  SYSOUT=*
//FT12F001 DD  SYSOUT=*
//SMF      DD  DSN=<YOUR SMF DATA>,DISP=SHR
DATA
  INTERVAL(KEEP=STEPEND  JOBNAME  STEPBEGN SHIFT STEPNAME
       PROGRAM  DATE  DEVADDR T30DD  EXCPS);
*********************************************************************;
LENGTH SHIFT $ 9;
LENGTH DSORG $ 2;
INFILE SMF COLUMN=C;
INPUT @2  RECID PIB1. @;

IF RECID NE 30 THEN DELETE;

  INPUT
         @03  STEPEND  SMFSTAMP8.
         @11  SYSID    $4.
         @19  SUBTYPE  PIB2.
         @21  SSOFF    PIB4.
         @25  SSLEN    PIB2.
         @27  SSNUM    PIB2.
         @29  IDOFF    PIB4.
         @33  IDLEB    PIB2.
         @35  IDNUM    PIB2.
         @93  EXOFF    PIB4.
         @97  EXLEB    PIB2.
         @99  EXNUM    PIB2. @;
FORMAT STEPEND STEPBEGN CLOSTIME DATETIME17.;

IF SUBTYPE NE 2 AND SUBTYPE NE 3 THEN DELETE;

SSOFF = SSOFF - 3;
IDOFF = IDOFF - 3;
EXOFF = EXOFF - 3;
INPUT @IDOFF    JOBNAME  $8.
      @IDOFF+8  PROGRAM  $8.
      @IDOFF+16 STEPNAME $8.
      @IDOFF+56 STEPBEGN SMFSTAMP8.
      @IDOFF+64 RDRTIME  $8. @;
IF JOBNAME NE 'LLA' THEN DELETE;
DATE=DATEPART(STEPEND);
FORMAT DATE DATE7.;
BEGHOUR=HOUR(STEPBEGN);
ENDHOUR=HOUR(STEPEND);

IF (BEGHOUR >8 AND BEGHOUR <17) OR
   (BEGHOUR < 9 AND ENDHOUR > 9) THEN SHIFT='PRIME';
ELSE SHIFT='OFF-PRIME';
*********************************************************************
       PROCESS  TYPE 30, SUBTYPE 2 (INTERVAL RECORDS)
           AND  TYPE 30, SUBTYPE 3 (STEP TERM RECORDS)
*********************************************************************
 ;
IF EXNUM = 0 THEN RETURN;
DISP = EXOFF;
DO I = 1 TO EXNUM;
  INPUT @DISP    UCBTYP3 $1.
        @DISP+2  DEVADDR PIB2.
        @DISP+4  T30DD   $8.
        @DISP+12 EXCPS   PIB4.
        @DISP+18 CONNECT PIB4. @;
  FORMAT DEVADDR CUA HEX4.;
  IF EXCPS > 0 THEN OUTPUT INTERVAL;
  DISP = DISP + EXLEB;
  END;
RETURN;
**************************************************************;
PROC SORT DATA=INTERVAL;
BY JOBNAME T30DD STEPBEGN;
PROC PRINT NOOBS ;
**************************************************************;
/*
