//SAS      EXEC SAS
//FT11F001 DD  SYSOUT=*
//FT12F001 DD  SYSOUT=*
//FT22F001 DD  SYSOUT=*
//SMF      DD  DSN=<YOUR SMF DATA>,DISP=SHR   <========
DATA RECS;
  INFILE SMF;
*******************************************************
*
*     SELECT RECORD TYPE 41 SUBTYPE 3 AND PREPARE
*     TO EXTRACT VLF STATISTICS
*
******************************************************* ;
  INPUT @2  RECID   PIB1. @;
  IF RECID EQ 41;
  INPUT @3  ENDED   SMFSTAMP8.
        @11 SYSID   $4.
        @19 SUBTYP  PIB2. @;
  IF SUBTYP EQ 3;
  INPUT @57 VLFOFF  PIB4.
        @61 VLFLEN  PIB2.
        @63 VLFCNT  PIB2. @;
  VLFOFF = VLFOFF - 3;
  DATE=DATEPART(ENDED);
  TIME=TIMEPART(ENDED);
  HOUR=HOUR(TIME);
*******************************************************
*
*     CUT ONE RECORD FOR EACH VLF CLASS
*
******************************************************* ;
IF VLFCNT > 0 THEN DO;
  OFFSET = VLFOFF;
  DO NDX = 1 TO VLFCNT;
    INPUT @OFFSET    CLASS    $8.
          @OFFSET+8  MAXVIRT  PIB4.
          @OFFSET+12 USEDVIRT PIB4.
          @OFFSET+16 SEARCHES PIB4.
          @OFFSET+20 HITS     PIB4.
          @OFFSET+24 ADDS     PIB4.
          @OFFSET+28 DELETES  PIB4.
          @OFFSET+32 TRIMS    PIB4.
          @OFFSET+36 MAXOBJCT PIB4. @;
    OFFSET=OFFSET+VLFLEN;
    MAXVIRT  = MAXVIRT  * 4096;
    USEDVIRT = USEDVIRT * 4096;
    OUTPUT;
  END;
END;
PROC SORT ;
  BY SYSID DATE CLASS HOUR;
PROC MEANS SUM MAX NOPRINT;
  VAR MAXVIRT USEDVIRT SEARCHES HITS
      ADDS DELETES TRIMS MAXOBJCT;
  OUTPUT OUT=SUM1
      SUM = S1 S2 SEARCHES HITS ADDS DELETES TRIMS S3
      MAX = MAXVIRT USEDVIRT M1 M2 M3 M4 M5 MAXOBJCT;
  BY SYSID DATE CLASS HOUR;
PROC PRINT NOOBS ;
  FORMAT DATE MMDDYY.
         MAXVIRT COMMA13. USEDVIRT COMMA13.
         SEARCHES COMMA13. HITS COMMA13. ADDS COMMA13.
         DELETES  COMMA13. TRIMS COMMA13. MAXOBJCT  COMMA13.;
  VAR CLASS HOUR MAXVIRT USEDVIRT SEARCHES HITS
                 ADDS DELETES TRIMS MAXOBJCT;
  SUM SEARCHES HITS ADDS DELETES TRIMS;
  BY SYSID DATE;
  TITLE1 'INFORMATION SYSTEMS';
  TITLE2 'VLF USAGE STATISTICS';
PROC MEANS DATA=RECS SUM MAX NOPRINT;
  VAR MAXVIRT USEDVIRT SEARCHES HITS
      ADDS DELETES TRIMS MAXOBJCT;
  OUTPUT OUT=SUM2
      SUM = S1 S2 SEARCHES HITS ADDS DELETES TRIMS S3
      MAX = MAXVIRT USEDVIRT M1 M2 M3 M4 M5 MAXOBJCT;
  BY SYSID DATE CLASS;
PROC PRINTTO UNIT=22;
PROC PRINT NOOBS ;
  FORMAT DATE MMDDYY.
         MAXVIRT COMMA13. USEDVIRT COMMA13.
         SEARCHES COMMA13. HITS COMMA13. ADDS COMMA13.
         DELETES  COMMA13. TRIMS COMMA13. MAXOBJCT  COMMA13.;
  VAR CLASS MAXVIRT USEDVIRT SEARCHES HITS
            ADDS DELETES TRIMS MAXOBJCT;
  SUM SEARCHES HITS ADDS DELETES TRIMS;
  BY SYSID DATE;
  TITLE1 'INFORMATION SYSTEMS';
  TITLE2 'VLF USAGE STATISTICS';
/*
